package de.md2.bestellung.controllers

main {
	appVersion "ATPS 2013"
	modelVersion "1.0"
	workflowManager backend
}

WorkflowElement bestellungWfe {
	defaultProcessChain defaultChain
	
	onInit {
		ereignisseRegistrieren,
		validatorenBinden
	}
	
	processChain defaultChain {
		step produktSuchen:
			view Suchen
		step produktBestellen:
			view Bestellen 
	}
	
	action CustomAction ereignisseRegistrieren {
		bind action produktLaden on Suchen.sucheBtn.onClick
		bind action bestellungAufgeben on Bestellen.bestellenBtn.onClick
	}
	
	action CustomAction validatorenBinden {
		bind validator RegExValidator(regEx "[a-z]+@[a-z]+\\.[a-z]+") 
			on Bestellen.bestellung[Bestellung.email]
	}
	
	action CustomAction produktLaden {
		call ContentProviderOperation(load :suchergebnis)
		call ContentProviderReset (:bestellungProvider)
		set :bestellungProvider.produkt = :suchergebnis
		
		call GotoView (Bestellen)
	}
	
	action CustomAction bestellungAufgeben {
		call ContentProviderOperation(save :bestellungProvider)
		call FireEvent (bestellungVollstaendig)
	}	
}

contentProvider Produkt suchergebnis {
	providerType backend
	filter first where Produkt.produktname equals Suchen.suchFeld
}

contentProvider Bestellung bestellungProvider {
	providerType backend
}

remoteConnection backend { 
	uri "http://wipc062.uni-muenster.de:8080/de.md2.bestellung.backend/service"
}