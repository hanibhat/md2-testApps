package de.wwu.md2.library.controllers

/*
 * Implement the controller here
 */
main {
	appVersion "0.1"
	modelVersion "1"
	workflowManager wipc062
}

remoteConnection wipc062 {
	uri "http://wipc062.uni-muenster.de:8080/de.wwu.md2.library.backend/service"
}

contentProvider Book bookProvider {
	providerType local
	filter first where Book.isbn equals MainView.bookInfoPart[Book.isbn]
}

contentProvider LoanRequest loanProvider {
	providerType local
}

contentProvider Book[] allBooksProvider {
	providerType local
	filter all
}

WorkflowElement LoanWfe {
	defaultProcessChain defaultChain
	
	onInit {
		bindEvents,
		mapFields
	}
	
	processChain defaultChain {
		step startChain:
			view MainView
	}
	
	action CustomAction bindEvents {
		bind action loadBook on MainView.ctrlPane.loadBookBtn.onClick
		bind action reset on MainView.ctrlPane.resetBtn.onClick
		bind action sendLoanRequest on MainView.loanBtn.onClick
	}
	
	action CustomAction mapFields {
		map MainView.loanEMail to :loanProvider.email
		// regex simplified and wrong
		bind validator RegExValidator(regEx ".+@.+\\.[a-z]+") on MainView.loanEMail
	}
	
	action CustomAction loadBook {
		call ContentProviderOperation(load :bookProvider)
		// New Object
		call ContentProviderReset(:loanProvider)
	}
	
	action CustomAction reset {
	    // New Object
		call ContentProviderReset(:bookProvider)
	}

	action CustomAction sendLoanRequest {
		set :loanProvider.bookRequested = :bookProvider
		call ContentProviderOperation(save :loanProvider)
		call FireEvent(endWorkflowEvent)
	}
}

WorkflowElement CreateBookWfe {
	defaultProcessChain defaultChain
	
	onInit {
		bindEvents
	}
	
	processChain defaultChain {
		step startChain:
			view CreateView
	}
	
	action CustomAction bindEvents {
		bind action createBook on CreateView.createBtn.onClick
	}
	
	action CustomAction createBook {
	    call ContentProviderOperation(save :bookProvider)
	    call FireEvent(endWorkflowEvent)
	}
}

WorkflowElement BooklistWfe {
	defaultProcessChain defaultChain
	
	onInit {
		bindEvents,
		loadBooks
	}
	
	action CustomAction loadBooks {
		call ContentProviderOperation(load :allBooksProvider)
	}
	
	action CustomAction bindEvents {
		bind action returnAction on BookList.doneBtn.onClick
	}
	
	action CustomAction returnAction {
	    call FireEvent(endWorkflowEvent)
	}
	
	processChain defaultChain {
		step startChain:
			view BookList
	}
}